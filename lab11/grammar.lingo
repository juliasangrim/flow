program = ws declaration*:d body:b {Program(:d, :b)};
declaration = "var" ws var:v ws ":" ws type:t ";" ws {Declaration(:v, :t)};

type = integer | array;
integer = "int" ws {TypeInt()};
array = "[" ws type:t "]" ws {TypeArray(:t)};

body = choiceExecs | seqExecs | loop | assign | test | print | while ;
loop = "*" ws body:b {Loop(:b)};
seqExecs = "{" ws body:b1 ";" ws body:b2 seqExec*:ss "}" ws {SeqExec(:b1, :b2, :ss)};
seqExec =  ";" ws body;
choiceExecs = "{" ws body:b1 ws "U" ws body:b2 choiceExec*:ss "}" ws {ChoiceExec(:b1, :b2, :ss)};
choiceExec = "U" ws body;

assign = var:v ws ":=" ws expression:e ws {Assign(:v, :e)};
print = "print(" ws expression:e ws ")" ws {Print(:e)};

test   = "(" ws expression:e1 condition$s ws expression:e2 ")" ws "?" ws {Test(:e1, $s, :e2)};
while  = "while" ws "(" ws expression:e1 condition$s ws expression:e2 ")" ws body:b {While(:e1, $s, :e2, :b)};

expression = sub | sum | mul | int | rat | inv | div | var;
sum = "(" ws expression:l ws "+" ws expression:r ws ")" ws {ArSum(:l, :r)};
mul = "(" ws expression:l ws "*" ws expression:r ws ")" ws {ArMult(:l, :r)};
sub = "(" ws expression:l ws "-" ws expression:r ws ")" ws {ArSub(:l, :r)};
rat = "(" ws int$n ws "/" ws int$d ws ")" ws {ArRat(s2i($n), s2i($d))};
div = "(" ws expression:l ws "/" ws expression:r ws ")" ws {ArDiv(:l, :r)};
inv =  "-" expression:val ws {ArInv(:val)};

int = digit+ $s {ArInt(s2i($s))};
digit = '0'-'9';


var = letter+ $s {ArVar($s)};
letter = 'a'-'z';
condition   = "<" | ">" | "==" | "!=" | "<=" | ">=";

ws = (' ' | '\t' | '\n' | '\r')*;
